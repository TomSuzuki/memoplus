;***** 画面のハードコピー印刷 (ghardcopy.hsp) *****

	#module
	;▼必要となるAPIや定数の定義
	#uselib "comdlg32.DLL"
	#cfunc  PrintDlg "PrintDlgA" var

	#uselib "gdi32.dll"
	#func   SetMapMode "SetMapMode" int,int
	#func   SetWindowOrgEx "SetWindowOrgEx" int,int,int,nullptr
	#func   SetViewportOrgEx "SetViewportOrgEx" int,int,int,nullptr
	#func   SetWindowExtEx "SetWindowExtEx" int,int,int,nullptr
	#func   SetViewportExtEx "SetViewportExtEx" int,int,int,nullptr
	#func   ScaleViewportExtEx "ScaleViewportExtEx" int,int,int,int,int,nullptr
	#cfunc  StartDoc "StartDocA" int,var
	#func   StartPage "StartPage" int
	#func   BitBlt "BitBlt" int,int,int,int,int,int,int,int,int
	#func   EndPage "EndPage" int
	#func   EndDoc "EndDoc" int
	#cfunc  GetDeviceCaps "GetDeviceCaps" int,int
	#func   DeleteDC "DeleteDC" int

	#deffunc ghardcopy int prm1
	;
	; nMode =1 : 用紙に合わせて拡大・縮小して印刷
	; nMode =0 : そのまま印刷
	;
	mref bmscr,67

	#define dispSizeX      bmscr(1)
	#define dispSizeY      bmscr(2)
	#define HORZRES        8
	#define VERTRES        10
	#define LOGPIXELSX     88
	#define LOGPIXELSY     90
	#define PD_RETURNDC    $00000100

	nMode = prm1 : if (nMode<=0)|(nMode>1) : nMode=0

	dim pd,20   ;PRINTDLG構造体

	pd(0)  = 66                         ;lStructSize
	pd(1)  = hwnd                       ;hwndOwner
	pd(2)  = 0                          ;hDevMode
	pd(3)  = 0                          ;hDevNames
	pd(5)  = PD_RETURNDC                ;Flags

	ret=PrintDlg(pd)
	PDhdc  = pd(4)                      ;PrinterHDC
	if ret!0 {
		pdevcaps=HORZRES,VERTRES,LOGPIXELSX,LOGPIXELSY
		repeat 4   : prnInfo(cnt)=GetDeviceCaps(PDhdc,pdevcaps(cnt)) : loop
		prnInfo(4)=GetDeviceCaps(hdc,pdevcaps(2))
		prnInfo(5)=GetDeviceCaps(hdc,pdevcaps(3))
	}else{
		return -1
	}
	SetMapMode PDhdc ,7 : SetWindowOrgEx PDhdc, 0,0
	SetViewportOrgEx PDhdc, 0,0
	SetWindowExtEx PDhdc, dispSizeX,dispSizeY
	if nMode=0 {
		SetViewportExtEx PDhdc, dispSizeX,dispSizeY
		ScaleViewportExtEx PDhdc, prnInfo(2), prnInfo(4), prnInfo(3), prnInfo(5)
	} else {
		SetViewportExtEx PDhdc, prnInfo(0),prnInfo(1)
	}
	;DOCINFO構造体
	dim dci,5 : pDOCNAME = "HSPDocument": dci= 20,varptr(pDOCNAME)
	ret=StartDoc(PDhdc,dci)
	if ret<0 { DeleteDC PDhdc : return -1 }
	StartPage PDhdc
	BitBlt PDhdc ,0,0,dispSizeX,dispSizeY, hdc, 0,0, $CC0020
	EndPage PDhdc : EndDoc PDhdc : DeleteDC PDhdc
	return
	#global

	;***** sample *****
	buffer 2
	picload "C:\\Users\\Shogo\\Music\\azuren\\a.png",1
	px=ginfo(12) : py=ginfo(13)
	gsel 0,1
	gmode 2 : pos 0,0:gcopy 2,0,0,px,py

	;印刷開始
	ghardcopy 0
	stop